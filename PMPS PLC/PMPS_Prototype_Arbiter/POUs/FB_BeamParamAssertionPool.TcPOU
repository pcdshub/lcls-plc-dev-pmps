<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.12">
  <POU Name="FB_BeamParamAssertionPool" Id="{1c7b2144-ac0c-0c05-3817-acc660914350}" SpecialFunc="None">
    <Declaration><![CDATA[(* This function block implements simple database. Data element values are stored in the hash table.  *)
FUNCTION_BLOCK FB_BeamParamAssertionPool
VAR_INPUT
	key 		: DWORD := 0;(* Entry key: used by A_Lookup, A_Remove method, the key variable is also used by A_Add method *)
	putPosPtr	: POINTER TO T_HashTableEntry := 0;(* Hash table entry position pointer (used by A_Find, A_GetNext, A_GetPrev) *)
	putValue 	: ST_BeamParams;(* Hash table entry value (used by A_AddHead, A_AddTail, A_Find )*)
END_VAR
VAR_OUTPUT
	bOk			: BOOL := FALSE;(* TRUE = Success, FALSE = Failed *)
	getPosPtr	: POINTER TO T_HashTableEntry := 0;(* Returned hash table entry position pointer *)
	getValue 	: ST_BeamParams;(* Returned hash table entry value *)
	nCount		: UDINT := 0;(* Hash table size (number of used entries, used by A_Count) *)
END_VAR
VAR
	dataPool	: ARRAY[0..PMPS_GVL_BeamParams.MAX_ASSERIONS] OF ST_BeamParams;(* Structured data element pool *)
	entries		: ARRAY[0..PMPS_GVL_BeamParams.MAX_ASSERIONS] OF T_HashTableEntry;(* Max. number of hash table entries. The value of table entry = 32 bit integer (pointer to dataPool-array-entry) *)
	fbTable 	: FB_HashTableCtrl;(* basic hash table control function block *)
	hTable 		: T_HHASHTABLE;(* hash table handle *)
	pRefPtr		: POINTER TO ST_BeamParams := 0;
	indexOfElem	: ULINT;(* Integer value (max. size: x86=>32bit, x64=>64bit)*)
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[;]]></ST>
    </Implementation>
    <Action Name="A_Add" Id="{7919a046-683b-0d93-13e3-2ac624ae17a2}">
      <Implementation>
        <ST><![CDATA[(* Adds entry to the table *)
MEMSET( ADR( getValue ), 0, SIZEOF( getValue ) );
getPosPtr := 0;

fbTable.A_Add( hTable := hTable, key := key, putValue := PMPS_GVL_BeamParams.cstSafeBeam(* we will set this value later *), getPosPtr=>getPosPtr, bOk=>bOk );(* Add new element to the table, getPosPtr points to the new entry *)
IF fbTable.bOk THEN(* Success *)
	fbTable.A_GetIndexAtPosPtr( hTable := hTable, putPosPtr := getPosPtr, getValue =>indexOfElem, bOk=>bOk );(* Get array index of getPosPtr entry *)
	IF fbTable.bOk THEN(* Success *)
		pRefPtr 	:= ADR( dataPool[indexOfElem] );(* Get pointer to the data element *)

		pRefPtr^ := putValue;(* copy application value *)

		fbTable.A_Add( hTable := hTable, key := key, putValue := pRefPtr, bOk=>bOk );(* Assign the entry value = pointer to the data element *)
		IF fbTable.bOk THEN(* Success *)
			getValue := putValue;
		END_IF
	END_IF
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_Count" Id="{5b68a7a9-68e9-040c-3c7e-89ff2c9c20a1}">
      <Implementation>
        <ST><![CDATA[(* Count number of used entries *)
nCount := hTable.nCount;
bOk := TRUE;]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_GetFirst" Id="{cef11d89-056c-0746-2586-17b2dd51306a}">
      <Implementation>
        <ST><![CDATA[(* Get first entry position pointer and value *)
MEMSET( ADR( getValue ), 0, SIZEOF( getValue ) );
getPosPtr := 0;

fbTable.A_GetFirst( hTable := hTable, putPosPtr := putPosPtr, getValue=>pRefPtr, getPosPtr=>getPosPtr, bOk=>bOk );
IF fbTable.bOk THEN
	getValue := pRefPtr^;
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_GetNext" Id="{67ccca5f-78e5-0ab2-39fd-98a465eafce1}">
      <Implementation>
        <ST><![CDATA[(* Get next entry position pointer and value *)
MEMSET( ADR( getValue ), 0, SIZEOF( getValue ) );
getPosPtr := 0;
bOk := FALSE;

IF putPosPtr = 0 THEN
	RETURN;
END_IF

fbTable.A_GetNext( hTable := hTable, putPosPtr := putPosPtr, getValue=>pRefPtr, getPosPtr=>getPosPtr, bOk=>bOk );
IF fbTable.bOk THEN
	getValue := pRefPtr^;
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_Lookup" Id="{541d5f50-42a3-0306-3819-6213b3e72829}">
      <Implementation>
        <ST><![CDATA[(* Lookup for entry by key  *)
MEMSET( ADR( getValue ), 0, SIZEOF( getValue ) );
getPosPtr := 0;

fbTable.A_Lookup( key := key, hTable := hTable, putPosPtr := putPosPtr, getValue=>pRefPtr, getPosPtr=>getPosPtr, bOk=>bOk );
IF fbTable.bOk THEN
	getValue := pRefPtr^;
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_Remove" Id="{86e9dccc-5522-0b97-29cd-c1bb2b777649}">
      <Implementation>
        <ST><![CDATA[(* Search for entry and remove it *)
MEMSET( ADR( getValue ), 0, SIZEOF( getValue ) );
getPosPtr := 0;

fbTable.A_Remove( key := key, hTable := hTable, putPosPtr := putPosPtr, getValue=>pRefPtr, getPosPtr=>getPosPtr, bOk=>bOk );
IF fbTable.bOk THEN
	getValue := pRefPtr^;
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_Reset" Id="{c708fc57-015a-05b4-28a5-0c08c25ec4ee}">
      <Implementation>
        <ST><![CDATA[(* Reset/initialize linked list *)
MEMSET( ADR( getValue ), 0, SIZEOF( getValue ) );
getPosPtr := 0;
bOk := F_CreateHashTableHnd( ADR( entries ), SIZEOF( entries ), hTable );(* Intialize table handle *)
fbTable.A_Reset( hTable := hTable, bOk=>bOk );
nCount := 0;]]></ST>
      </Implementation>
    </Action>
    <LineIds Name="FB_BeamParamAssertionPool">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_BeamParamAssertionPool.A_Add">
      <LineId Id="2" Count="16" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_BeamParamAssertionPool.A_Count">
      <LineId Id="2" Count="1" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_BeamParamAssertionPool.A_GetFirst">
      <LineId Id="2" Count="6" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_BeamParamAssertionPool.A_GetNext">
      <LineId Id="2" Count="11" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_BeamParamAssertionPool.A_Lookup">
      <LineId Id="2" Count="6" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_BeamParamAssertionPool.A_Remove">
      <LineId Id="2" Count="6" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_BeamParamAssertionPool.A_Reset">
      <LineId Id="2" Count="4" />
      <LineId Id="1" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>